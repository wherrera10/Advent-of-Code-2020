using Primes

function iscircularprime(n)
    if isprime(n)
        dig, ten = digits(n), zero(n) + 10
        for i in 1:length(dig)-1
            m = evalpoly(ten, circshift(dig, i))
            (m < n || !isprime(m)) && return false
        end
        return true
    end
    return false
end

function findcircular(N)
    i, circprimeseq = 2, Int[]
    while length(circprimeseq) < N
        if iscircularprime(i)
            push!(circprimeseq, i)
        end
        i += 1
    end
    return circprimeseq
end

function allonescircularprime()
    count = 0
    for i in 6:100000
        n = BigInt(evalpoly(BigInt(10), ones(Int, i)))
        if isprime(n)
            println("R($i)")
            (count += 1) >= 4 && return
        end
    end
end

println("The first 19 circular primes are:\n", findcircular(19))
println("\nThe next 4 circular primes, in repunit format, are:")
allonescircularprime()
println("\nChecking larger repunits:")
for i in [5003, 9887, 15073, 25031, 35317, 49081]
    n = BigInt(evalpoly(BigInt(10), ones(Int, i)))
    if isprime(n)
        println("R($i) is prime")
    end
end

