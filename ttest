# chacocipher

@enum Mode ENCRYPT DECRYPT
const leftalphabet = Vector{Char}("HXUCZVAMDSLKPEFJRIGTWOBNYQ")
const rightalphabet = Vector{Char}("PTLNBQDEOYSFAVZKGJRIHWXUMC")

rotate(s, n, start=0, finish=0) =
    start == 0 ? circshift(s, -n) :
        vcat(s[1:start], circshift(s[start+1:finish], -n), s[finish+1:end])

rightpermute(s, n) = rotate(rotate(s, n + 1), 1, 2, 13)
leftpermute(s, n) = rotate(rotate(s, n), 1, 1, 13)

function chacocoding(text, mode, showsteps=false)
    len = length(text)
    left, right = Vector{Char}(leftalphabet), Vector{Char}(rightalphabet)
    etext = fill(' ', len)
    for i in 1:len
        showsteps && println(String(left), " ", String(right))
        if mode == ENCRYPT
            idx = indexin(text[i], right)[1]
            etext[i] = left[idx]
        else
            idx = indexin(text[i], left)[1]
            etext[i] = right[idx]
        end
        if i < len
            left = leftpermute(left, idx)
            right = rightpermute(right, idx)
        end
    end
    String(etext)
end

function testchacocipher()
    plaintext = "WELLDONEISBETTERTHANWELLSAID"
    println("The original plaintext is : $plaintext")
    println("\nThe left and right alphabets after each permutation",
             " during encryption are :\n")
    ciphertext = chacocoding(plaintext, ENCRYPT, true)
    println("\nThe ciphertext is : $ciphertext")
    plaintext2 = chacocoding(ciphertext, DECRYPT)
    println("\nThe recovered plaintext is : $plaintext2")
end

testchacocipher()

The original plaintext is : WELLDONEISBETTERTHANWELLSAID

The left and right alphabets after each permutation during encryption are :

HXUCZVAMDSLKPEFJRIGTWOBNYQ PTLNBQDEOYSFAVZKGJRIHWXUMC
BYQHXUCZVAMDNSLKPEFJRIGTWO UMPTLNBQDEOYCSFAVZKGJRIHWX
MNSLKPEFJRIGDTWOBYQHXUCZVA YCFAVZKGJRIHSWXUMPTLNBQDEO
XCZVAMNSLKPEUFJRIGDTWOBYQH BQEOYCFAVZKGDJRIHSWXUMPTLN
HCZVAMNSLKPEXUFJRIGDTWOBYQ BQOYCFAVZKGDEJRIHSWXUMPTLN
XFJRIGDTWOBYUQHCZVAMNSLKPE JRHSWXUMPTLNIBQOYCFAVZKGDE
ZAMNSLKPEXFJVRIGDTWOBYUQHC CFVZKGDEJRHSAWXUMPTLNIBQOY
YQHCZAMNSLKPUEXFJVRIGDTWOB BQYCFVZKGDEJORHSAWXUMPTLNI
PEXFJVRIGDTWUOBYQHCZAMNSLK ORSAWXUMPTLNHIBQYCFVZKGDEJ
BQHCZAMNSLKPYEXFJVRIGDTWUO QYFVZKGDEJORCSAWXUMPTLNHIB
XJVRIGDTWUOBFQHCZAMNSLKPYE WXMPTLNHIBQYUFVZKGDEJORCSA
OFQHCZAMNSLKBPYEXJVRIGDTWU YUVZKGDEJORCFSAWXMPTLNHIBQ
NLKBPYEXJVRISGDTWUOFQHCZAM ORFSAWXMPTLNCHIBQYUVZKGDEJ
RSGDTWUOFQHCIZAMNLKBPYEXJV NCIBQYUVZKGDHEJORFSAWXMPTL
VSGDTWUOFQHCRIZAMNLKBPYEXJ NCBQYUVZKGDHIEJORFSAWXMPTL
ZMNLKBPYEXJVASGDTWUOFQHCRI ORSAWXMPTLNCFBQYUVZKGDHIEJ
NKBPYEXJVASGLDTWUOFQHCRIZM AWMPTLNCFBQYXUVZKGDHIEJORS
EJVASGLDTWUOXFQHCRIZMNKBPY NCBQYXUVZKGDFHIEJORSAWMPTL
QCRIZMNKBPYEHJVASGLDTWUOXF EJRSAWMPTLNCOBQYXUVZKGDFHI
MKBPYEHJVASGNLDTWUOXFQCRIZ MPLNCOBQYXUVTZKGDFHIEJRSAW
YHJVASGNLDTWEUOXFQCRIZMKBP OBYXUVTZKGDFQHIEJRSAWMPLNC
ZKBPYHJVASGNMLDTWEUOXFQCRI PLCOBYXUVTZKNGDFQHIEJRSAWM
XQCRIZKBPYHJFVASGNMLDTWEUO RSWMPLCOBYXUAVTZKNGDFQHIEJ
KPYHJFVASGNMBLDTWEUOXQCRIZ OBXUAVTZKNGDYFQHIEJRSWMPLC
ZPYHJFVASGNMKBLDTWEUOXQCRI OBUAVTZKNGDYXFQHIEJRSWMPLC
XCRIZPYHJFVAQSGNMKBLDTWEUO MPCOBUAVTZKNLGDYXFQHIEJRSW
HFVAQSGNMKBLJDTWEUOXCRIZPY TZNLGDYXFQHIKEJRSWMPCOBUAV
JTWEUOXCRIZPDYHFVAQSGNMKBL EJSWMPCOBUAVRTZNLGDYXFQHIK

The ciphertext is : OAHQECBKOEUMVJIMYFZPIOZIOYLQ

The recovered plaintext is : WELLDONEISBETTERTHANWELLSAID

  
